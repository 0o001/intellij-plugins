{
  parserClass = "name.kropp.intellij.makefile.MakefileParser"
  extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix = "Makefile"
  psiImplClassSuffix = "Impl"
  psiPackage = "name.kropp.intellij.makefile.psi"
  psiImplPackage = "name.kropp.intellij.makefile.psi.impl"

  elementTypeHolderClass = "name.kropp.intellij.makefile.psi.MakefileTypes"
  elementTypeClass = "name.kropp.intellij.makefile.psi.MakefileElementType"
  tokenTypeClass = "name.kropp.intellij.makefile.psi.MakefileTokenType"

  tokens = [
    COLON = ":"
    SEMICOLON = ";"
    ASSIGN = "="
    PIPE = "|"
    KEYWORD_INCLUDE = "include"
    KEYWORD_MINUSINCLUDE = "-include"
    KEYWORD_IFEQ = "ifeq"
    KEYWORD_IFNEQ = "ifneq"
    KEYWORD_IFNDEF = "ifndef"
    KEYWORD_ELSE = "else"
    KEYWORD_ENDIF = "endif"
    KEYWORD_DEFINE = "define"
    KEYWORD_ENDEF = "endef"
  ]

  psiImplUtilClass = "name.kropp.intellij.makefile.psi.MakefilePsiImplUtil"
}

makefile ::= (rule|variable|directive|comment)*

private directive ::= define|include

rule ::= target_line (';' EOL? | EOL) recipe
recipe ::= (conditional|commands)?

target_line ::= targets ':' prerequisites {pin=2 methods=[getTargetName]}
targets ::= target+
target ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefileNamedElementImpl" implements="name.kropp.intellij.makefile.psi.MakefileNamedElement" methods=[getName setName getNameIdentifier]}

prerequisites ::= normal_prerequisites ('|' order_only_prerequisites)?
normal_prerequisites ::= prerequisite*
order_only_prerequisites ::= prerequisite+
prerequisite ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefilePrerequisiteMixin"}

commands ::= command*

conditional ::= ('ifeq'|'ifneq'|'ifndef') condition thenbranch ('else' elsebranch)* 'endif'

thenbranch ::= commands
elsebranch ::= commands

variable ::= variable_name ('='|':='|'::='|'?='|'!='|'+=') variable_value? {pin=2}
variable_name ::= identifier
define ::= 'define' variable_name ('='|':='|'::='|'?='|'!='|'+=')? variable_value_line* 'endef'

include ::= ('include'|'-include'|'sinclude') filename+ EOL
filename ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefileFilenameMixin"}
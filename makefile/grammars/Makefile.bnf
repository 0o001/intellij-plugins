{
  parserClass = "name.kropp.intellij.makefile.MakefileParser"
  extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix = "Makefile"
  psiImplClassSuffix = "Impl"
  psiPackage = "name.kropp.intellij.makefile.psi"
  psiImplPackage = "name.kropp.intellij.makefile.psi.impl"

  elementTypeHolderClass = "name.kropp.intellij.makefile.psi.MakefileTypes"
  elementTypeClass = "name.kropp.intellij.makefile.psi.MakefileElementType"
  tokenTypeClass = "name.kropp.intellij.makefile.psi.MakefileTokenType"

  tokens = [
    COLON = ":"
    SEMICOLON = ";"
    ASSIGN = "="
    PIPE = "|"
    KEYWORD_INCLUDE = "include"
    KEYWORD_IFEQ = "ifeq"
    KEYWORD_IFNEQ = "ifneq"
    KEYWORD_IFNDEF = "ifndef"
    KEYWORD_ELSE = "else"
    KEYWORD_ENDIF = "endif"
    KEYWORD_ENDEF = "endef"
    KEYWORD_DEFINE = "define"
    KEYWORD_UNDEFINE = "undefine"
    KEYWORD_OVERRIDE = "override"
    KEYWORD_EXPORT = "export"
    KEYWORD_PRIVATE = "private"
    KEYWORD_VPATH = "vpath"
  ]

  psiImplUtilClass = "name.kropp.intellij.makefile.psi.MakefilePsiImplUtil"
}

makefile ::= (rule|variable-assignment|directive|topconditional|comment)*

private directive ::= define|include|undefine|override|export|privatevar|vpath

rule ::= target_line (';' comment? EOL? | comment? EOL) recipe
recipe ::= (conditional|command)*
command ::= command-line (line-split command-line)*

target_line ::= targets ':' prerequisites {pin=2 methods=[getTargetName]}
targets ::= target+
target ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefileNamedElementImpl" implements="name.kropp.intellij.makefile.psi.MakefileNamedElement,com.intellij.navigation.NavigationItem" methods=[getName setName getNameIdentifier getPresentation]}

prerequisites ::= normal_prerequisites ('|' order_only_prerequisites)?
normal_prerequisites ::= prerequisite*
order_only_prerequisites ::= prerequisite+
prerequisite ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefilePrerequisiteMixin"}

conditional ::= ('ifeq'|'ifneq'|'ifndef') condition comment? branch ('else' comment? branch)* 'endif' comment? {pin=1}

branch ::= (conditional|command)*

meta variable-assignment ::= variable assignment variable-value? {pin=2}
private assignment ::= ('='|':='|'::='|'?='|'!='|'+=')
variable ::= identifier
meta variable-value ::= variable-value-line (line-split variable-value-line)*
define ::= 'define' variable assignment? variable-value-line* 'endef' {pin=1 methods=[getAssignment getValue]}
undefine ::= 'undefine' variable comment? EOL {pin=1}
override ::= 'override' variable-assignment comment? {pin=1}
export ::= 'export' (variable-assignment|variable)? comment? {pin=1}
privatevar ::= 'private' variable-assignment comment? {pin=1}

include ::= ('include'|'-include'|'sinclude') filename+ EOL {pin=1}
filename ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefileFilenameMixin"}

vpath ::= 'vpath' (pattern directory*)? comment? EOL {pin=1}
pattern ::= identifier
directory ::= identifier {mixin="name.kropp.intellij.makefile.psi.impl.MakefileFilenameMixin"}

topconditional ::= ('ifeq'|'ifneq'|'ifndef') condition comment? block ('else' comment? block)* 'endif' comment? {pin=1}
block ::= variable-assignment|directive|if|comment
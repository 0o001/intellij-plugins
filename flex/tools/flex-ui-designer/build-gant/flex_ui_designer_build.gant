// this script should be executed *after* project compiled, as it uses classes of 'flex-ui-designer' module

// this is called to build artifacts to be used when running from sources
target('default': "Build Flex UI Designer artifacts") {
  home = guessHome(this)
  home = "$home/../../.."; // IDEA\flex\tools\flex-ui-designer is detected
  doExecute("$home", "$home/out", "$home/out/classes/production/flex-ui-designer")
}

binding.setVariable("includeFile", {String filepath ->
  Script s = groovyShell.parse(new File(filepath))
  s.setBinding(binding)
  s
})

def guessHome(Script script) {
  File home = new File(script["gant.file"].substring("file:".length()))

  while (home != null) {
    if (home.isDirectory()) {
      if (new File(home, ".idea").exists()) return home.getCanonicalPath()
    }
    home = home.getParentFile()
  }
  return null
}

def execute(home, sandbox, targetFolder) {
  if (isDefined("pluginFilter")) {
    if (!pluginFilter.contains("flex-ui-designer")) return
  }
  project.stage("building Flex library for flex-ui-designer plugin")
  doExecute(home, sandbox, targetFolder)
}

class SwfDescriptor {
  String config;
  String basedir;
  boolean isSwc;

  SwfDescriptor(String config, String basedir, boolean isSwc) {
    this.config = config;
    this.basedir = basedir;
    this.isSwc = isSwc;
  }
}


private def doExecute(home, sandbox, targetFolder) {
  flexBuildHome = "$home/flex/tools/flex-ui-designer/build-gant";
  flexSandbox = "$sandbox/ui-designer"

  ant.delete(dir: "$flexSandbox")
  ant.mkdir(dir: "$flexSandbox")

  ant.copy(toDir: "$flexSandbox/repo", overwrite: "true") {
    fileset(dir: "$flexBuildHome/../build/repo")
  }

  List<SwfDescriptor> swfDescriptors = includeFile("$flexBuildHome/flex_ui_designer_swfs_build.gant").getList();
  swfDescriptors.each {
    if (it.config.startsWith("flex-injection-") || it.config.equals("shared.xml")) {
      buildFlex(it.config, it.basedir, flexBuildHome, flexSandbox, it.isSwc)
    }
  };

  compileAbcBuilder(sandbox)
  invokeAbcBuilder(sandbox, "4.1")
  invokeAbcBuilder(sandbox, "4.5")

  buildAssets()

  swfDescriptors.each {
    if (!it.config.startsWith("flex-injection-") && !it.config.equals("shared.xml")) {
      buildFlex(it.config, it.basedir, flexBuildHome, flexSandbox, it.isSwc);
    }
  };

  ant.copy(file: "$flexSandbox/app-loader-1.0-SNAPSHOT.swf", tofile: "$targetFolder/designer.swf")
  ant.copy(file: "$flexBuildHome/../designer/src/main/resources/descriptor.xml", todir: "$targetFolder")
  ant.copy(file: "$flexBuildHome/../designer/src/main/resources/check-descriptor.xml", todir: "$targetFolder")
  ant.copy(file: "$flexSandbox/flex-injection-4.1.abc", todir: "$targetFolder")
  ant.copy(file: "$flexSandbox/flex-injection-4.5.abc", todir: "$targetFolder")
}

private def buildAssets() {
  ant.java(className: "org.flyti.assetBuilder.AssetBuilderMojo", fork: "true", failOnError: "true") {
    classpath {
      pathelement(path: "$flexBuildHome/libs/asset-builder-maven-plugin-1.4-SNAPSHOT.jar")
      pathelement(path: "$flexBuildHome/libs/snakeyaml-1.8-s2.jar")
      pathelement(path: "$flexBuildHome/libs/plexus-utils-2.0.6.jar")
      pathelement(path: "$flexBuildHome/libs/jai-core-1.1.3.jar")
      pathelement(path: "$flexBuildHome/libs/jai-codec-1.1.3.jar")
      pathelement(path: "$home/community/plugins/maven/maven2-server-impl/lib/maven-2.2.1-uber.jar")
    }
    arg(value: "$flexBuildHome/../plaf/aqua/src/main/resources/assets.yml") // descriptor
    arg(value: "$flexSandbox/assets") // output file
    arg(value: "$flexBuildHome/../plaf/aqua/src/main/resources") // sources
  }
}

private def compileAbcBuilder(sandbox) {
  ant.javac(srcdir: "$flexBuildHome/../abc-builder-maven-module/src/main/java", destdir: "$flexSandbox", fork: "yes") {
    classpath {
      pathelement(path: "$sandbox/classes/production/flex-ui-designer")
      pathelement(path: "$home/community/plugins/maven/maven2-server-impl/lib/maven-2.2.1-uber.jar")
    }
  }
}

private def invokeAbcBuilder(sandbox, flexVersion) {
  ant.java(className: "com.intellij.flex.uiDesigner.AbcBuilderMojo", fork: "true", failOnError: "true") {
    classpath {
      pathelement(path: "$flexSandbox")
      pathelement(path: "$sandbox/classes/production/flex-ui-designer")
      pathelement(path: "$sandbox/classes/production/util")
      pathelement(path: "$sandbox/classes/production/xml")
      pathelement(path: "$home/community/plugins/maven/maven2-server-impl/lib/maven-2.2.1-uber.jar")
      pathelement(path: "$home/community/lib/trove4j.jar")
    }
    arg(value: "$flexSandbox") // folder
    arg(value: "$flexVersion") // flex version
  }
}

def buildFlex(config, basedir, flexBuildHome, flexSandbox, isSwc) {
  ant.copy(file: "$flexBuildHome/flex-configs/${config}", toDir: "$flexSandbox", overwrite: "true")

  ant.replace(file: "$flexSandbox/${config}") {
    replacefilter(token: "@@target@@", value: "$flexSandbox")
    replacefilter(token: "@@source@@", value: "$flexBuildHome/../$basedir/src/main")
    replacefilter(token: "@@repo@@", value: "$flexSandbox/repo")
    replacefilter(token: "@@configs@@", value: "$flexBuildHome/flex-configs")
  }

  replacement = flexBuildHome.replaceAll("\\\\", "/") + "/../\\1/src/main"
  ant.replaceregexp(file: "$flexSandbox/${config}", byline: "true") {
    regexp(pattern: "@@source:(.*)@@")
    substitution(expression: "$replacement")
  }

  ant.java(className: "${isSwc ? "flex2.tools.Compc" : "flex2.tools.Mxmlc"}", fork: "true", failOnError: "true") {
    jvmarg(value: "-Dsun.io.useCanonCaches=false")
    jvmarg(value: "-Duser.language=en")
    jvmarg(value: "-Duser.region=en")
    jvmarg(value: "-Dflex.swf.uncompressed=true")
    classpath {
      fileset(dir: "$flexBuildHome/compiler-libs");
    }
    arg(value: "-load-config=$flexSandbox/${config}")
  }
}

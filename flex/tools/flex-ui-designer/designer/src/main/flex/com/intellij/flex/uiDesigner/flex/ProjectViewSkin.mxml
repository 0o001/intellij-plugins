<?xml version="1.0"?>
<Skin xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns="cocoa" xmlns:tools="com.intellij.flex.uiDesigner.flex.tools.*">
  <fx:Metadata>
    [HostComponent("com.intellij.flex.uiDesigner.flex.ProjectView")]
  </fx:Metadata>
  <fx:Script><![CDATA[
    import cocoa.Insets;
    import cocoa.plaf.TabViewSkin;

    import mx.core.IUIComponent;

    override protected function createChildren():void {
      super.createChildren();

      var editorTabViewContentInsets:Insets = TabViewSkin(editorTabView.skin).contentInsets;
      editorToolbar.skin.move(editorTabViewContentInsets.left, 28 + 25 /* tab title bar */);
    }

    override protected function measure():void {
      // skip
    }

    override protected function updateDisplayList(w:Number, h:Number):void {
      var topToolbarHeight:Number = 28;
      toolbar.skin.setActualSize(w, topToolbarHeight);

      var editorTabViewUI:IUIComponent = editorTabView.skin;

      var sidebarUI:IUIComponent = sidebar.skin;
      sidebarUI.y = topToolbarHeight;
      editorTabViewUI.y = topToolbarHeight;
      var sidebarWidth:Number = sidebarUI.getExplicitOrMeasuredWidth();
      var editorTabViewWidth:Number = sidebarUI.x = w - sidebarWidth;
      sidebarUI.setActualSize(sidebarWidth, h - topToolbarHeight);
      editorTabViewUI.setActualSize(editorTabViewWidth, h - topToolbarHeight);

      var editorTabViewContentInsets:Insets = TabViewSkin(editorTabViewUI).contentInsets;
      editorToolbar.skin.setActualSize(editorTabViewWidth - editorTabViewContentInsets.width, 25);

      elementToolContainer.setOffset(editorTabViewContentInsets);

      var g:Graphics = graphics;
      g.clear();
      g.beginFill(0xededed);
      g.drawRect(0, 0, w, h);
      g.endFill();
    }
    ]]></fx:Script>

  <Toolbar id="toolbar" height="28"/>
  <TabView id="editorTabView" lafSubkey="ProjectView"/>
  <Toolbar id="editorToolbar" height="25" lafSubkey="Editor">
    <layout>
      <BarLayout padding="5" gap="12"/>
    </layout>
    <SegmentedControl id="statesBar"/>
    <SegmentedControl id="elementTreeBar" itemRenderer="com.intellij.flex.uiDesigner.flex.ElementTreeBarItemRenderer"/>
  </Toolbar>
  <tools:ElementToolContainer id="elementToolContainer"/>

  <Sidebar id="sidebar"/>
</Skin>
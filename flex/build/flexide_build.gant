import org.jetbrains.jps.Jps
import org.jetbrains.jps.LayoutInfo
import org.jetbrains.jps.Module
import static org.jetbrains.jps.idea.IdeaProjectLoader.guessHome

includeTargets << new File("${guessHome(this)}/community/build/scripts/utils.gant")
includeTargets << new File("${guessHome(this)}/build/scripts/libLicenses.gant")
includeTool << Jps

def product = "Astella"

requireProperty("buildNumber", requireProperty("build.number", snapshot))
setProperty("product", "${product}")
setProperty("buildPrefix", "${product}-EAP-")
setProperty("buildName", "${buildPrefix}${buildNumber}")
requireProperty("launchProperties", "")


// load ApplicationInfo.xml properties
ant.xmlproperty(file: "$home/flex/ideResources/idea/FlexApplicationInfo.xml", collapseAttributes: "true")

setProperty("system_selector", "Astella${p("component.version.major")}0")

setProperty("ch", "$home/community")
setProperty("dryRun", false)
setProperty("jdk16", guessJdk())
setProperty("dontClean", false)

setProperty("pluginFilter", [
            "htmltools",
            "JavaScriptLanguage",
            "cvsIntegration", "cvs-core", "cvs-plugin", "javacvs-src", "smartcvs-src",
            "PerforceIntegration", "Perforce", "perforce_util",
            "svn4ideaOld",
            "git4idea",
            "github",
            "hg4idea",
            "CSS", "css-api",
            "JSInspections",
            "JSIntentionPowerPack",
            "less", "sass",
            "tasks", "tasks-core", "tasks-api", "jira-connector",
            "JavaScriptDebugger", "FirefoxConnector", "ChromeConnector",
            "flex-plugin", "flex-ui-designer", "flex-profiler", "properties",
            "ant"/*, "maven", "maven-server-api", "maven2-server-impl", "maven3-server-impl"*/
            ])

private List platformImplementationModules() {
  return [
          "core-impl",
          "RegExpSupport",
          "license_common",
          "platform-ultimate",
          "platform-impl",
          "vcs-impl",
          "lang-impl",
          "testRunner",
          "smRunner",
          "xdebugger-impl",
          "testFramework",
          "tests_bootstrap",
          "xml",
          "relaxng",
          "lvcs-impl",
          "images",
          "spellchecker",
          "yaml",
          "haml",
          "graph",
          "duplicates",
          //"coverage-impl",
          "dom-impl",
          "notNull",
          "java-impl",
          "compiler-impl",
          "debugger-impl",
          "execution-impl",
          "dom-impl",
          "idea-ui",
          "jsp-spi",
          "java-psi-impl"
  ]
}

private List<String> platformApiModules() {
  return ["core-api", "platform-api", "lvcs-api", "lang-api", "vcs-api", "usageView", "xdebugger-api", "xml-openapi", "dom-openapi", "webide-api",
          "openapi", "testFramework-java", "debugger-openapi", "compiler-openapi", "execution-openapi", "jsp-openapi", "jsp-base-openapi",
          "java-psi-api"]
}

class Paths {
  final sandbox
  final distAll
  final distWin
  final distMac
  final distLinux
  final artifacts
  final ideaSystem
  final ideaConfig

  def Paths(String home) {
    sandbox = "$home/out/flex-ide"
    distAll = "$sandbox/layout"
    distWin = "$sandbox/win"
    distMac = "$sandbox/mac"
    distLinux = "$sandbox/linux"

    ideaSystem = "$sandbox/system"
    ideaConfig = "$sandbox/config"

    artifacts = "$sandbox/artifacts"
  }
}

setProperty("paths", new Paths(home))

target('default': "Build artifacts") {

  def modules = [
          "flex-ide", "resources", "colorSchemes", platformApiModules(), platformImplementationModules(), pluginFilter
  ].flatten()

  loadProject()

  processLaunchProperties() {
    pair -> project.info("launch property: " + pair["key"] + "=" + pair["value"])
  }

  project.targetFolder = "$paths.sandbox/classes"
  project.dryRun = dryRun

  if (!dryRun && !dontClean) {
    project.stage("cleanup")

    def sandbox = paths.sandbox
    ant.delete(dir: sandbox)
    ant.mkdir(dir: sandbox)
  }

  ant.tstamp() {
    format(property: "todayYear", pattern: "yyyy")
  }

  ant.patternset(id: "resources.included") {
    include(name: "**/*.properties")
    include(name: "fileTemplates/**/*")
    include(name: "inspectionDescriptions/**/*")
    include(name: "intentionDescriptions/**/*")
    include(name: "tips/**/*")
    include(name: "search/**/*")
  }

  ant.patternset(id: "resources.excluded") {
    exclude(name: "**/*.properties")
    exclude(name: "fileTemplates/**/*")
    exclude(name: "fileTemplates")
    exclude(name: "inspectionDescriptions/**/*")
    exclude(name: "inspectionDescriptions")
    exclude(name: "intentionDescriptions/**/*")
    exclude(name: "intentionDescriptions")
    exclude(name: "tips/**/*")
    exclude(name: "tips")
  }

  if (!dryRun) {
    zipSources(home, paths.artifacts)
    reportArtifact("sources.zip")
  }

  def usedJars = new HashSet();

  // TODO I'm not sure yFiles jars matter
  def forbiddenJars = ["client-api", "ideaLicenseDecoder", "eawtstub.jar", "/ui.jar", "exe4jlib.jar", "/y.jar", "ysvg.jar"]

  if (!dontClean) {
    project.clean()
  }

  modules.each {
    Module module = project.modules[it] as Module
    if (module != null) {
      project.info("Building $module.name")
      module.runtimeClasspath().each {
        File file = new File(it)
        if (file.exists()) {
          String path = file.canonicalPath.replace('\\', '/')
          String normalizedHome = home.replace('\\', '/')
          if (path.endsWith(".jar") && (path.startsWith("$normalizedHome/lib/") ||
                                        path.startsWith("$normalizedHome/community/lib/"))
              && !path.contains("/dev/") && !path.contains("/rt/") && !path.contains("/ant/")
              && !forbiddenJars.any { path.contains(it) }) {
            usedJars.add(path);
          }
        }
      }
      module.make()
    }
    else {
      project.warning("$it is not a module")
    }
  }

  // script will do nothing if 'flex-ui-designer' plugin is not part of pluginFilter
  def flex_ui_designer_build_script = includeFile("$home/flex/tools/flex-ui-designer/build-gant/flex_ui_designer_build.gant")
  flex_ui_designer_build_script.execute("$home", "$paths.sandbox", "$home/out/flex-ide/classes/production/flex-ui-designer")

  // script will do nothing if 'flex-profiler' plugin is not part of pluginFilter
  def flex_profiler_build_script = includeFile("$home/flex/tools/profiler/build-gant/flex_profiler_build.gant")
  flex_profiler_build_script.execute("$home", "$paths.sandbox", "$home/out/flex-ide/classes/production/flex-profiler")

  usedJars.add("$home/community/lib/looks-2.2.2.jar")

  buildSearchableOptions()

  ant.copy(file: "$home/flex/build/flexide.exe.vmoptions", tofile: "$paths.sandbox/astella.exe.vmoptions")

  processLaunchProperties() {
    pair -> ant.echo(file: "$paths.sandbox/astella.exe.vmoptions", append: "true", message: "-D${pair["key"]}=${pair["value"]}\r\n")
  }

  wireBuildDate(buildNumber)
  LayoutInfo layoutInfo = layoutFull(paths.distAll, usedJars)
  generateLicensesTable("$paths.artifacts/third-party-libraries.txt", layoutInfo.usedModules);

  // TODO let's turn scrambling off for now
  //scramble()

  layoutWin(paths.distWin)
  layoutMac(buildName: buildName,
            version: "1.0",
            system_selector: system_selector,
            paths.distMac)

  ant.echo(message: "${product}-${buildNumber}", file: "$paths.sandbox/layout/build.txt")

  buildSit()
  buildDmg()

  layoutLinux(paths.distLinux)
  buildTarGz()

  buildExe()
  buildWinZip()

  buildNsis()
}

private def processLaunchProperties(Closure processor) {
  if (launchProperties != "") {
    launchProperties.tokenize(";").each {
      tokens = it.tokenize("=");
      processor(key: tokens[0], value: tokens[1]);
    }
  }
}

private def buildWinZip() {
  ant.zip(zipfile: "$paths.artifacts/${buildName}.zip") {
    fileset(dir: paths.distAll)
    fileset(dir: paths.distWin)
  }
  reportArtifact("${buildName}.zip")
}

private def buildSit() {
  project.stage("sit")
  def root = isEap() ? "${buildName}.app" : "${product}-${p("component.version.major")}.${p("component.version.minor")}.app"

  ant.zip(zipfile: "${paths.artifacts}/${buildName}.sit") {
    tarfileset(dir: paths.distAll, prefix: root)
    tarfileset(dir: paths.distMac, prefix: root) {
      exclude(name: "bin/*.sh")
      exclude(name: "bin/idea.properties")
      exclude(name: "Contents/MacOS/idea")
    }
    tarfileset(dir: paths.distMac, filemode: "755", prefix: root) {
      include(name: "bin/*.sh")
      include(name: "Contents/MacOS/idea")
    }
  }
  reportArtifact("${buildName}.sit")
}

private def buildDmg() {
  project.stage("dmg")
  ant.copy(file: "${paths.artifacts}/${buildName}.sit", tofile: "${paths.artifacts}/${product}${buildNumber}.sit") // to comply with buildDmg.xml naming
  executeExternalAnt("file.name": "${product}", "dmg.image.name": "$home/flex/ideResources/artwork/fiDmgBg.png", "$home/build/buildDmg.xml")

  ant.delete(file: "${paths.artifacts}/${product}${buildNumber}.sit")
  ant.move(file: "${paths.artifacts}/${product}${buildNumber}.dmg", tofile: "${paths.artifacts}/${buildName}.dmg")
  reportArtifact("${buildName}.dmg")
}


private def buildNsis() {
  if (!isMac()) {
    project.stage("nsis")
    def installer = includeFile("$home/build/scripts/nsis_installer.gant")
    installer.nsis_installer([paths.distAll, paths.distWin],
                             "$home/flex/build/strings.nsi",
                             "$home/flex/build/paths.nsi",
                             "${buildPrefix}", false, true, system_selector, true)
    ant.available(file: "${paths.artifacts}/${buildName}.exe", property: "nsis.exists");
    ant.fail(unless: "nsis.exists", message: "${buildName} NSIS EXE was not built. File ${paths.artifacts}/${buildName}.exe wasn't found.")
    reportArtifact("${buildName}.exe")
  }
  else {
    project.warning("Building nsis .exe distribution skipped")
  }
}

def buildSearchableOptions() {
  project.stage("searchable options")
  def dest_dir = "$paths.sandbox/searchableoptions"
  ant.mkdir(dir: "${dest_dir}/search")
  ant.copy(file: "$home/build/idea.license", todir: paths.ideaSystem)
  //ant.copy(file: "$home/build/rubymine.license", todir: paths.ideaSystem)
  ant.java(classname: "com.intellij.idea.Main", fork: "true") {
    jvmarg(line: "-ea -Xmx250m -Didea.home.path=$home -Didea.system.path=${paths.ideaSystem} -Didea.config.path=${paths.ideaConfig} -Didea.platform.prefix=Flex -Didea.no.jre.check=true")
    jvmarg(value: "-Xbootclasspath/a:${boot.output}")
    arg(line: "traverseUI ${dest_dir}/search/searchableOptions.xml")

    classpath() {
      pathelement(location: "$ch/lib/junit.jar")
      project["flex-ide"].runtimeClasspath().each {
        pathelement(location: it)
      }
      pathelement(location: "${jdkHome}/lib/tools.jar")
    }
  }
  ant.available(file: "${dest_dir}/search/searchableOptions.xml", property: "searchableoptions.exists");
  ant.fail(unless: "searchableoptions.exists", message: "${product} searchable options were not built.")
}

def zkmScramble(String script, String dir, String jarName) {
  ant.java(classname: "ZKM", fork: "true", failonerror: "true") {
    jvmarg(value: "-Xmx1100m")
    arg(value: "-v")
    arg(value: script)
    classpath {
      fileset(dir: dir) {
        include(name: "*.jar")
        exclude(name: jarName)
      }

      fileset(dir: "$home/lib") {
        include(name: "*.jar")
      }

      pathelement(location: "$home/build/lib/ZKM.jar")
    }
  }

  ant.delete(file: "$dir/${jarName}.BACKUP", failonerror: "false")
}

def scramble() {
  ant.copy(file: "$home/build/conf/script.zkm.stub", tofile: "$paths.sandbox/script.zkm", overwrite: "true") {
    filterset(begintoken: "__", endtoken: "__") {
      filter(token: "CLASSES", value: "\"${paths.distAll}/lib/flexide.jar\"")
      filter(token: "SCRAMBLED_CLASSES", value: "${paths.distAll}/")
      filter(token: "INCREMENTAL", value: "")
    }
  }
  ant.mkdir(dir: "$paths.artifacts/flexide.unscrambled")
  ant.copy(file: "$paths.distAll/lib/flexide.jar", todir: "$paths.artifacts/flexide.unscrambled")
  zkmScramble("$paths.sandbox/script.zkm", "$paths.distAll/lib", "flexide.jar")
  ant.move(file: "$paths.distAll/flexide.jar", todir: "$paths.distAll/lib")
  ant.zip(destfile: "$paths.artifacts/logs.zip") {
    fileset(file: "ChangeLog.txt")
    fileset(file: "ZKM_log.txt")
    fileset(file: "$paths.sandbox/script.zkm")
  }
  ant.delete(file: "ChangeLog.txt")
  ant.delete(file: "ZKM_log.txt")
}

String appInfoFile() {
  return "$home/out/flex-ide/classes/production/flex-ide/idea/FlexApplicationInfo.xml"
}

void wireBuildDate(String buildNumber) {
  if (!dryRun) {
    ant.replace(token: "__BUILD_NUMBER__", value: buildNumber, file: appInfoFile())
    ant.replace(token: "__BUILD_DATE__", value: DSTAMP, file: appInfoFile())
  }
}

def buildExe() {
  if (!isMac()) {
    ant.mkdir(dir: paths.distWin)
    executeExternalAnt(
            "config.file": "$home/flex/build/flex-ide.exe4j",
            "output.file": "astella.exe",
            "system_selector" : system_selector,
            "$home/build/buildLauncher.xml")
  }
  else {
    project.warning("Building .exe executable skipped")
  }
}

def layoutLinux(String target) {
  layout(target) {
    dir("bin") {
      renamedFile("$paths.sandbox/astella.exe.vmoptions", "astella.vmoptions")

      fileset(dir: "$home/flex/build") {
        include(name: "idea.properties")
      }

      renamedFile("$home/flex/build/flexide.sh", "astella.sh")
      renamedFile("$home/flex/ideResources/artwork/flexide.png", "astella.png")

      fileset(dir: "$home/community/bin") {
        include(name: "log.xml")
      }
      fileset(dir: "$home/community/bin/nix") {
        include(name: "inspect*.sh")
      }
    }
    dir("help") {
      fileset(dir: "$home/flex/keymap") {
        include(name: "ReferenceCard.pdf")
      }
      fileset(dir: "$home/out/help") {
        include(name: "help.jar")
      }
    }
  }

  patchPropertiesFile(target)

  ant.replace(file: "$target/bin/astella.sh") {
    replacefilter(token: "@@isEap@@", value: isEap())
    replacefilter(token: "@@system_selector@@", value: system_selector)
  }
  ant.fixcrlf(srcDir: "$target/bin", excludes: "*.png", eol: "unix")
}

def buildTarGz() {
  project.stage("tar.gz")
  def tarRoot = isEap() ? "${buildName}" : "${product}-${p("component.version.major")}.${p("component.version.minor")}"
  def tarPath = "$paths.artifacts/${buildName}.tar"
  ant.tar(tarfile: tarPath) {
    tarfileset(dir: paths.distAll, prefix: tarRoot)
    tarfileset(dir: "$home/bin", prefix: "$tarRoot/bin") {
      include(name: "libyjpagent*.so")
    }
    tarfileset(dir: "$home/community/bin/linux", prefix: "$tarRoot/bin") {
      exclude(name: "libbreakgen*")
      exclude(name: "fsnotifier*")
    }
    tarfileset(dir: "$home/community/bin/linux", filemode: "755", prefix: "$tarRoot/bin") {
      include(name: "fsnotifier*")
    }
    tarfileset(dir: "$paths.distLinux", filemode: "755", prefix: "$tarRoot") {
      include(name: "bin/*.sh")
    }
    tarfileset(dir: "$paths.distLinux", prefix: "$tarRoot") {
      exclude(name: "bin/*.sh")
    }
  }

  ant.gzip(src: tarPath, zipfile: "${tarPath}.gz")
  ant.delete(file: tarPath)
  reportArtifact("${buildName}.tar.gz")
}

def layoutFull(String target, Set usedJars) {
  def implementationModules = [platformImplementationModules(), "flex-ide"].flatten()

  def result = layout(target) {
    dir("lib") {
      jar("util.jar") { module("util") }

      jar("openapi.jar") {
        platformApiModules().each { module it}
      }

      jar("annotations.jar") { module("annotations")}
      jar("extensions.jar") { module("extensions")}

      jar("flexide.jar") {
        implementationModules.each {
          module(it) {
            exclude(name: "**/common/**.rb")
            exclude(name: "**/tips/**")
            exclude(name: "/search/**")
            exclude(name: "**/FileTypeManager.xml")
          }
        }
        zipfileset(src: "${home}/lib/client-api.jar")
        zipfileset(src: "${home}/lib/ideaLicenseDecoder.jar")
      }

      jar("bootstrap.jar") { module("bootstrap")}
      jar("resources.jar") {
        module("platform-resources") {
          exclude(name: "**/FileTypeManager.xml")
        }
        module("resources") {
          exclude(name: "**/preview/**")
          exclude(name: "**/JavaActions.xml")
          exclude(name: "**/JBuilderKeymap.xml")
          exclude(name: "**/Palette2.xml")
          exclude(name: "**/PropertiesComponent.xml")
          exclude(name: "**/IdeaPlugin.xml")
          exclude(name: "**/javadoc/**")
          exclude(name: "**/liveTemplates/**")
          exclude(name: "**/ClassTemplateManager.xml")
          exclude(name: "**/DebuggerSettings.xml")
        }
        module("colorSchemes")
        module("flex-ide") {
          include(name: "**/FileTypeManager.xml")
        }
      }

      jar("forms_rt.jar") {
        module("forms_rt")
      }

      jar([name: "resources_en.jar", duplicate: "preserve"]) {
        // Flex IDE resources come first to override platform resources (using duplicate=preserve)
        fileset(dir: "$home/flex/ideResources") {
          include(name: "**/tips/**")
        }
        fileset(dir: "$paths.sandbox/searchableoptions")
        module("platform-resources-en")
        module("resources-en") {
          ant.patternset() {
            include(name: "**/AntBundle.properties")
            include(name: "**/CompilerBundle.properties")
            include(name: "**/DebuggerBundle.properties")
            include(name: "**/JavaErrorMessages.properties")
            include(name: "**/EvaluationFeedbackRequest.html")
          }
        }
      }

      jar("icons.jar") { module("icons") }
      jar("boot.jar") { module("boot") }

      jar("jps-server.jar") {
        module("jps-builders")
        module("jps-model")
      }

      usedJars.each {
        fileset(file: it)
      }

      fileset(dir: "$home/community/xml/relaxng/lib", includes: "*.jar")

      fileset(dir: "$home/community/lib/src") {
        include(name: "trove4j_changes.txt")
        include(name: "trove4j_src.jar")
      }
    }

    //dir("help") {
    //  fileset(dir: "$home/ruby/help") {
    //    include(name: "*.pdf")
    //  }
    //}

  dir("license") {
      renamedFile("$home/flex/license/FlexIDE_license.txt", "Astella_license.txt")
  }

    def layouts = includeFile("$home/build/scripts/layouts.gant")
    println("layout plugins\n\n")

    setProperty("buildFlexToolPlugins", true)
    layouts.layoutPlugins()
  }
  return result
}

private def noResources(String moduleName) {
  module(moduleName) {
    patternset(refid: "resources.excluded")
  }
}

private def resources(List<String> modules) {
  jar("resources_en.jar") {
    modules.each {
      module(it) {
        patternset(refid: "resources.included")
      }
    }
  }
}

def layoutMac(Map args, String target) {
  layout(target) {
    dir("bin") {
      fileset(dir: "$home/bin", includes: "*.jnilib")

      fileset(dir: "$home/community/bin") {
        include(name: "log.xml")
      }

      fileset(dir: "$home/flex/build") {
        include(name: "idea.properties")
      }
    }
    dir("help") {
      fileset(dir: "$home/flex/keymap") {
        include(name: "ReferenceCardForMac.pdf")
      }
      renamedFile("$home/out/help/help_mac.jar", "help.jar")
    }
  }

  patchPropertiesFile(target)

  ant.fixcrlf(srcDir: "$target/bin", includes: "*.sh", eol: "unix")

  args["buildNumber"] = buildNumber
  args["executable"] = "astella"
  args["icns"] = "$home/flex/ideResources/artwork/flexide.icns"
  args["bundle_name"] = "Astella"
  args["bundleIdentifier"] = "com.jetbrains.flexide"
  args["platform_prefix"] = "Flex"
  args["idea.properties.path"] = "$target/bin/idea.properties"

  Map launchProps = new HashMap()
  processLaunchProperties() {
    pair -> launchProps[pair["key"]] = pair["value"]
  }
  if (!launchProps.empty) {
    args["idea.properties"] = launchProps
  }

  layoutMacApp(target, "$home/community", args)
}

def layoutWin(String target) {
  layout(target) {
    dir("bin") {
      fileset(dir: "$home/bin") {
        include(name: "yjpagent.dll")
      }
      fileset(dir: "$home/community/bin/win") {
        exclude(name: "breakgen*")
        exclude(name: "idea.bat")
      }
      fileset(dir: "$home/flex/build") {
        include(name: "idea.properties")
      }
      renamedFile("$home/flex/build/flexide.bat", "astella.bat")

      fileset(file: "$paths.sandbox/astella.exe.vmoptions")

      fileset(dir: "$home/community/bin") {
        include(name: "log.xml")
      }
    }
    dir("help") {
      fileset(dir: "$home/flex/keymap") {
        include(name: "ReferenceCard.pdf")
      }
      fileset(dir: "$home/out/help") {
        include(name: "help.jar")
      }
    }
  }

  patchPropertiesFile(target)

  ant.replace(file: "$target/bin/astella.bat") {
    replacefilter(token: "@@system_selector@@", value: system_selector)
  }

  ant.fixcrlf(srcDir: "$target/bin", excludes: "*.dll *.exe", eol: "dos")
}

def set(Map args, String key, Object value) {
  if (!args.containsKey(key)) {
    args[key] = value
  }
}

def executeExternalAnt(Map args, String file) {
  set(args, "build.number", buildNumber)
  set(args, "project.home", home)
  set(args, "artifacts.path", paths.artifacts)
  set(args, "dist.win", paths.distWin)
  //set(args, "dist.all", paths.distAll)

  //set(args, "dist.all.15", paths.distAll15)
  //set(args, "dist.all.16", paths.distAll16)
  set(args, "sandbox.path", "$paths.sandbox/sandbox")
  set(args, "component.version.major", getProperty("component.version.major"))
  set(args, "component.version.minor", "\"" + getProperty("component.version.minor") + "\"")

  ant.java(failonerror: "true", classname: "org.apache.tools.ant.Main", fork: "true") {
    args.each { String key, String value ->
      jvmarg(line: "-D$key=$value")
    }

    jvmarg(line: "-Xmx250m")

    classpath {
      fileset(dir: "$home/build/lib") {
        include(name: "commons-net-1.4.1.jar")
        include(name: "jsch-0.1.43.jar")
      }

      fileset(dir: "$ch/lib/ant/lib") {
        include(name: "*.jar")
      }

      fileset(file: "$jdk16/lib/tools.jar")
      fileset(file: "$ch/lib/oromatcher.jar")
    }

    arg(value: "-f")
    arg(value: file)
  }
}

def reportArtifact(fileMask) {
  // Force TC to show artifact on-the-fly
  ant.echo(message: "##teamcity[publishArtifacts '$paths.artifacts/${fileMask}']")
}
